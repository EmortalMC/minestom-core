plugins {
    id("java-library")
    id("maven-publish")

    id("com.github.johnrengelman.shadow") version "7.1.2"
}

group "cc.towerdefence.minestom"
version "1.0-SNAPSHOT"

repositories {
    mavenCentral()
    mavenLocal() // In maven local: openmatch.frontend
    maven { url = "https://repo.towerdefence.cc/snapshots" }
    maven { url = "https://repo.towerdefence.cc/releases" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    api("cc.towerdefence.api:agones-sdk:1.0.3")
    api("cc.towerdefence.api:grpc-sdk:e0756e1")
    api("io.kubernetes:client-java:16.0.1")

    api("com.github.Minestom:Minestom:7eb9d3fdd7")
    implementation("net.kyori:adventure-text-minimessage:4.11.0")
//    api("net.minestom.server:Minestom:1.0") // local

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.9.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.9.0")
}

test {
    useJUnitPlatform()
}

shadowJar {
    mergeServiceFiles()
}

publishing {
    repositories {
        maven {
            name = "development"
            url = uri("https://repo.towerdefence.cc/snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_SECRET")
            }
        }
        maven {
            name = "release"
            url = uri("https://repo.towerdefence.cc/releases")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_SECRET")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = "cc.towerdefence.minestom"
            artifactId = "core"

            def commitHash = System.getenv("COMMIT_HASH_SHORT")
            def releaseVersion = System.getenv("RELEASE_VERSION")
            version = commitHash ?: releaseVersion ?: "local"

            from components.java
        }
    }
}